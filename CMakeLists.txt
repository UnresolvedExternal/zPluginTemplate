cmake_minimum_required(VERSION 3.21)

set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/")
set(CMAKE_POLICY_VERSION_MINIMUM 3.5)

set(PROJECT_VERSION "1.0.0.0" CACHE STRING "Project version")

include(target_add_versioninfo)

get_filename_component(PROJECT_NAME_FROM_DIR "${CMAKE_CURRENT_SOURCE_DIR}" NAME)

project(${PROJECT_NAME_FROM_DIR}
	DESCRIPTION "Union plugin for Gothic Games"
	VERSION ${PROJECT_VERSION}
)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# By default template uses dynamically linked union-api target to avoid compatibility problems.
# If you want to produce a standalone plugin, set this option to OFF
set(GENERATE_UNION_API_DLL ON)

# By default template generates .vdf with the .dll and files form \vdf\_Work and \vdf\System
# If you want to produce only the .dll, set this option to OFF
set(GENERATE_VDF ON)

# By default, if GENERATE_VDF is ON, template copies generated .vdf volume to Plugins subfolder
# If you want to manage the plugin loading by yourself, set this option to OFF
# Also, make sure that EXE_NAME value in CMakePresets.json is valid Gothic executable
set(COPY_VDF_TO_GAME ON)

if (GENERATE_UNION_API_DLL AND NOT GENERATE_VDF)
	message(WARNING "GENERATE_UNION_API_DLL is ON but GENERATE_VDF is OFF. UnionAPI.dll won't be included in the release.")
endif()

file(GLOB_RECURSE SRC
    "src/*.cpp"
	"resources/*.h"
	"resources/*.rc"
)

add_library(${CMAKE_PROJECT_NAME} SHARED)
target_sources(${CMAKE_PROJECT_NAME} PRIVATE ${SRC})
target_precompile_headers(${CMAKE_PROJECT_NAME} PRIVATE "src/stdafx.h")
target_compile_definitions(${CMAKE_PROJECT_NAME} PRIVATE "PROJECT_NAME=\"${CMAKE_PROJECT_NAME}\"")

target_include_directories(${CMAKE_PROJECT_NAME}
    INTERFACE
		"include/"

	PRIVATE
		BEFORE "userapi/"
		"src/"
		"resources/"
)

target_add_versioninfo(${CMAKE_PROJECT_NAME}
	VERSION "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}.${PROJECT_VERSION_TWEAK}"
	COMPANY_NAME "Gothic Community"
	PRODUCT_NAME "${CMAKE_PROJECT_NAME}"
)

if (GENERATE_VDF)
	set(VDF_DIR "${PROJECT_SOURCE_DIR}/vdf/")
	set(VDF_BUILD_DIR "${CMAKE_BINARY_DIR}/vdf/")
	set(VDF_BUILD_AUTORUN_DIR "${VDF_BUILD_DIR}System/Autorun/")

	file(COPY "${VDF_DIR}" DESTINATION "${VDF_BUILD_DIR}" FOLLOW_SYMLINK_CHAIN)

	set(VDF_BUILD_VM_FILE "${VDF_BUILD_DIR}build.vm")
	configure_file("${VDF_DIR}build.vm" "${VDF_BUILD_VM_FILE}")

	string(REPLACE "/" "\\" VDF_BUILD_VM_FILE "${VDF_BUILD_VM_FILE}")

	if (GENERATE_UNION_API_DLL)
		add_custom_target(copy_union_api_dll
			ALL
				COMMAND ${CMAKE_COMMAND} -E make_directory ${VDF_BUILD_AUTORUN_DIR}
				COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:union_api_dll> ${VDF_BUILD_AUTORUN_DIR}
				COMMAND ${CMAKE_COMMAND} -E echo "[Post Build] $<TARGET_FILE_NAME:union_api_dll> copied to: ${VDF_BUILD_AUTORUN_DIR}"
			DEPENDS union_api_dll
		)
	endif()

	add_custom_command(TARGET ${CMAKE_PROJECT_NAME}
		POST_BUILD
			COMMAND ${CMAKE_COMMAND} -E make_directory ${VDF_BUILD_AUTORUN_DIR}
			COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:${CMAKE_PROJECT_NAME}> ${VDF_BUILD_AUTORUN_DIR}
			COMMAND ${CMAKE_COMMAND} -E echo "[Post Build] $<TARGET_FILE_NAME:${CMAKE_PROJECT_NAME}> copied to: ${VDF_BUILD_AUTORUN_DIR}")

	add_custom_target(build_vdf
		ALL
			COMMAND ${CMAKE_COMMAND} -E echo "[VDF] Building ${CMAKE_PROJECT_NAME}.vdf ..."
			COMMAND "${VDF_BUILD_DIR}/GothicVDFS.exe" /B "${VDF_BUILD_VM_FILE}"
			COMMAND ${CMAKE_COMMAND} -E echo "[VDF] ${CMAKE_PROJECT_NAME}.vdf build successfully!"
		WORKING_DIRECTORY "${VDF_BUILD_DIR}"
		DEPENDS ${CMAKE_PROJECT_NAME}
	)

	if (COPY_VDF_TO_GAME)
		if (EXISTS "${EXE_PATH}")
			cmake_path(GET EXE_PATH PARENT_PATH PLUGINS_DIR)
			cmake_path(GET PLUGINS_DIR PARENT_PATH PLUGINS_DIR)
			cmake_path(APPEND PLUGINS_DIR "Data/Plugins")

			add_custom_command(TARGET build_vdf
				POST_BUILD
					COMMAND ${CMAKE_COMMAND} -E echo "[VDF] Copying ${CMAKE_PROJECT_NAME}.vdf to ${PLUGINS_DIR}..."
					COMMAND ${CMAKE_COMMAND} -E copy "${VDF_BUILD_DIR}/${CMAKE_PROJECT_NAME}.vdf" "${PLUGINS_DIR}/${CMAKE_PROJECT_NAME}.vdf"
					COMMAND ${CMAKE_COMMAND} -E echo "[VDF] ${CMAKE_PROJECT_NAME}.vdf copied successfully!"
			)
		else()
			add_custom_command(TARGET build_vdf
				POST_BUILD
					COMMAND ${CMAKE_COMMAND} -E echo "[VDF] ${EXE_PATH} does not exist"
			)
		endif()
	endif()
endif()

add_subdirectory(dependencies)